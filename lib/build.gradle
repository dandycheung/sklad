apply plugin: 'com.android.library'

android {

    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'BUILD_DIR', "\"$buildDir.absolutePath\""
        }

        debug {
            buildConfigField 'String', 'BUILD_DIR', "\"$buildDir.absolutePath\""
        }

    }

    lintOptions {
        disable 'InvalidPackage'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

}

def okhttpVersion = '3.4.2'
def robolectricVersion = "3.1.4"

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:support-annotations:27.0.2'
    implementation 'com.squareup.okhttp3:okhttp:' + okhttpVersion

    testImplementation 'junit:junit:4.12'

    testImplementation 'org.assertj:assertj-core:3.8.0'
    testImplementation 'org.mockito:mockito-core:2.8.47'

    testImplementation "org.robolectric:robolectric:" + robolectricVersion
    testImplementation "org.robolectric:robolectric-annotations:" + robolectricVersion

    testImplementation 'com.squareup.okhttp3:mockwebserver:' + okhttpVersion

    testImplementation 'commons-io:commons-io:2.5'

}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "passed", "failed"
        showStandardStreams true
    }
}
